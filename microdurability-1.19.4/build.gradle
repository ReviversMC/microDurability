version += '+1.19.4'

dependencies {
	include implementation(project(path: ':microdurability-core', configuration: 'namedElements'))

	minecraft "com.mojang:minecraft:${project.minecraft_version_1_19_4}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings_1_19_4}:v2"

	// Required Fabric API modules
	modImplementation(fabricApi.module('fabric-api-base', project.fabric_api_version_1_19_4))
	modImplementation(fabricApi.module('fabric-rendering-v1', project.fabric_api_version_1_19_4))

	// Other mods
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version_1_19_4}"
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version_1_19_4}") {
		exclude(group: 'net.fabricmc.fabric-api')
	}


	if (project.use_third_party_mods == 'true') {
		configurations {
			modRuntimeOnly {
				exclude module: 'fabric-rendering-v1'
			}
		}

		// Use full Fabric API bundle while debugging, since transitive deps aren't always declared correctly
		modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version_1_19_4}"

		modRuntimeOnly "maven.modrinth:sodium:${project.sodium_version_1_19_4}"
		modRuntimeOnly "maven.modrinth:lithium:${project.lithium_version_1_19_4}"
		modRuntimeOnly "maven.modrinth:starlight:${project.starlight_version_1_19_4}"
		modRuntimeOnly "maven.modrinth:ferrite-core:${project.ferritecore_version_1_19_4}"
		modRuntimeOnly "curse.maven:smooth-boot-415758:${project.smoothboot_version_1_19_4}"
		modRuntimeOnly "curse.maven:no-fade-452768:${project.no_fade_version_1_19_4}"
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}
