plugins {
	id 'fabric-loom'
	id 'checkstyle'
	id 'com.diffplug.spotless'
}


int width = 960;
int height = 480;

try {
	java.awt.GraphicsDevice gd = java.awt.GraphicsEnvironment.getLocalGraphicsEnvironment().getDefaultScreenDevice();
	width = Math.max(width, (int) (gd.getDisplayMode().getWidth() * 0.5));
	height = Math.max(height, (int) (gd.getDisplayMode().getHeight() * 0.5));
} catch (java.awt.HeadlessException e) {
	// Ignore
} catch (Throwable e) {
	e.printStackTrace()
}


allprojects {
	apply plugin: 'fabric-loom'
	apply plugin: 'checkstyle'
	apply plugin: 'com.diffplug.spotless'

	checkstyle {
		configFile = rootProject.file('checkstyle.xml')
		toolVersion = project.checkstyle_version
	}

	spotless {
		java {
			removeUnusedImports()
			importOrder('java', 'javax', '', 'net.minecraft', 'com.github.reviversmc')
			indentWithTabs()
			trimTrailingWhitespace()
		}
	}


	version = rootProject.mod_version
	group = rootProject.maven_group

	base {
		archivesName = project.archives_base_name
	}


	configurations {
		modRuntimeOnly {
			transitive = true
			exclude module: 'fabric-loader'
		}
	}

	repositories {
		maven {
			name = 'Modrinth'
			url = 'https://api.modrinth.com/maven'
			content {
				includeGroup 'maven.modrinth'
			}
		}
		if (project.use_third_party_mods == 'true') {
			maven {
				url = 'https://cursemaven.com'
				content {
					includeGroup 'curse.maven'
				}
			}
		}
		maven {
			name = 'TerraformersMC'
			url = 'https://maven.terraformersmc.com/releases'
		}
		maven {
			url = 'https://maven.shedaniel.me/'
		}
		mavenCentral()
	}

	dependencies {
		modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
	}

	loom {
		runs {
			client {
				programArg '--width'
				programArg "$width"

				programArg '--height'
				programArg "$height"
			}
		}
		mixin {
			useLegacyMixinAp = false
		}
	}


	processResources {
		inputs.property 'version', project.version

		filesMatching('fabric.mod.json') {
			expand 'version': project.version
		}
	}

	tasks.withType(JavaCompile).configureEach {
		// Ensure that the encoding is set to UTF-8, no matter what the system default is.
		// This fixes some edge cases with special characters not displaying correctly,
		// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html.
		// If Javadoc is generated, this must be specified in that task too.
		it.options.encoding = 'UTF-8'
	}

	java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
		// if it is present. If you remove this line, sources will not be generated.
		withSourcesJar()

		// If this mod is going to be a library, then it should also generate Javadocs in order to aid
		// with development. Uncomment this line to generate them.
		// withJavadocJar()
	}

	jar {
		from rootProject.file('LICENSE')
	}

	build.doFirst {
		use_third_party_mods = 'false'
	}
}


dependencies {
	include api(project(path: ':microdurability-core', configuration: 'namedElements'))
	include api(project(path: ':microdurability-1.17', configuration: 'namedElements'))
	include api(project(path: ':microdurability-1.19', configuration: 'namedElements'))
	include api(project(path: ':microdurability-1.19.4', configuration: 'namedElements'))
	include api(project(path: ':microdurability-1.20', configuration: 'namedElements'))
	include api(project(path: ':microdurability-1.20.5', configuration: 'namedElements'))

	modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version_1_20_5}"
	modRuntimeOnly("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version_1_20_5}") {
		exclude(group: 'net.fabricmc.fabric-api')
	}

	minecraft "com.mojang:minecraft:${minecraft_version_1_20_5}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings_1_20_5}:v2"
}


processResources {
	from("${rootDir}/assets/icon.png") {
		into "assets/microdurability/"
	}
}
